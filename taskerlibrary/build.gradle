apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    github = 'https://github.com/kevindmoore/tasker'
    githubUrl = 'https://github.com/kevindmoore/tasker.git'
    projName = 'taskerlibrary'
    projDescription = 'Library for UI and background tasks'
    currentVersionCode = 7
    currentVersion = '1.0.7'
    groupId = 'com.mastertechsoftware.tasker'
}

repositories {
    maven {
        url  "https://dl.bintray.com/kevindmoore/tasker"
    }
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode currentVersionCode
        versionName currentVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}


task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    classpath += project.files(configurations.compile)
    failOnError false
}


task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs


artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name projName
            description projDescription
            url github
            inceptionYear '2016'

            packaging 'aar'
            groupId groupId
            artifactId projName
            version currentVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection githubUrl
                url github

            }
            developers {
                developer {
                    name 'Kevin Moore'
                }
            }
        }
    }
}

group = 'com.mastertechsoftware.tasker'
version = currentVersion


bintray {
    user = bintray_user
    key = bintray_key
    dryRun = false //Whether to run this as dry-run, without deploying
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = projName

        labels = ['android']
        licenses = ['Apache-2.0']
        vcsUrl = githubUrl
        websiteUrl = github
    }
}
